buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url = 'https://www.cursemaven.com'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mcversion}-${version}"
group = 'thecodex6824.tcresearchpatcher' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'ThaumcraftResearchPatcher'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mcversion}-${forgeversion}"
    runDir = 'run'
    useDepAts = true
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = 'stable_39'
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    replace '@VERSION@', project.version
    replaceIn 'TCResearchPatcherContainer.java'
    
    if (project.hasProperty('keyStore')) {
        replace '@FINGERPRINT@', project.certFingerprint
        replaceIn 'TCResearchPatcherContainer.java'
    }
    else {
        replace '@FINGERPRINT@', ""
        replaceIn 'TCResearchPatcherContainer.java'
    }
    
    clientJvmArgs += ('-Dfml.coreMods.load=thecodex6824.tcresearchpatcher.TCResearchPatcher' + System.getProperty("line.separator"))
    serverJvmArgs += ('-Dfml.coreMods.load=thecodex6824.tcresearchpatcher.TCResearchPatcher' + System.getProperty("line.separator"))
}

dependencies {
	deobfCompile 'curse.maven:thaumcraft-223628:2629023'
    
	runtime 'curse.maven:baubles-227083:2518667'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { project.hasProperty('keyStore') }
    
    if (project.hasProperty('keyStore')) { // needed to skip gradle trying to find vars
        keyStore = project.keyStore
        alias = project.alias
        storePass = project.storePass
        keyPass = project.keyPass
        
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar

jar {
    manifest {
        attributes 'FMLCorePlugin': 'thecodex6824.tcresearchpatcher.TCResearchPatcher'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'forgeversion', project.forgeversion
    
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'forgeversion':project.forgeversion
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}
